#   Copyright (C) 2001 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.  

# Define libstdc++-v3 callbacks for dg.exp.

load_lib dg.exp
load_lib libgloss.exp

proc libstdc++-v3-init { args } {
    global srcdir
    global outdir
    global blddir
    global cxx
    global includes
    global cxxflags
    global objdir
    global gluefile wrap_flags
    global ld_library_path

    set blddir   [lookfor_file $outdir libstdc++-v3]

   # By default, we assume we want to run program images.
    global dg-do-what-default
    set dg-do-what-default run

    # Copy any required data files.
    foreach tst [glob -nocomplain "$srcdir/*/*.tst"] {
	file copy -force $tst $outdir
    }
    foreach txt [glob -nocomplain "$srcdir/*/*.txt"] {
	file copy -force $txt $outdir
    }

    # set LD_LIBRARY_PATH so that libgcc_s, libstdc++ binaries can be found.
    set gccpath [get_multilibs]
    set ld_library_path "."
    append ld_library_path ":${gccpath}../gcc"
    append ld_library_path ":${blddir}/src/.libs"

    # Do a bunch of handstands and backflips for cross compiling and
    # finding simulators...
    if [is_remote host] {
        set header [remote_download host ${blddir}/testsuite/testsuite_hooks.h]
        if { $header == "" } {
            verbose -log "Unable to download ${blddir}/testsuite/testsuite_hooks.h to host."
            return "untested"
        }
      set cxx [transform "g++"]
      set cxxflags "-ggdb3 -DDEBUG_ASSERT"
        set includes "-I./"
    } else {
        # If we find a testsuite_flags file, we're testing in the build dir.
        set flags_file "${blddir}/testsuite_flags"
        if { [file exists $flags_file] } {
            set cxx [exec sh $flags_file --build-cxx]
            set cxxflags [exec sh $flags_file --cxxflags]
            set includes [exec sh $flags_file --build-includes]
        } else {
            set cxx [transform "g++"]
            set cxxflags "-ggdb3 -DDEBUG_ASSERT"
            set includes "-I${srcdir}"
        }
    }

    if { [target_info needs_status_wrapper]!="" && ![info exists gluefile] } {
      set gluefile ${objdir}/testglue.o;
      set result [build_wrapper $gluefile];
      if { $result != "" } {
          set gluefile [lindex $result 0];
          set wrap_flags [lindex $result 1];
      } else {
          unset gluefile
      }
    }
}

proc libstdc++-v3-dg-test { prog do_what extra_tool_flags } {
    # Set up the compiler flags, based on what we're going to do.

    switch $do_what {
	"preprocess" {
	    set compile_type "preprocess"
	    set output_file "[file rootname [file tail $prog]].i"
	}
	"compile" {
	    set compile_type "assembly"
	    set output_file "[file rootname [file tail $prog]].s"
	}
	"assemble" {
	    set compile_type "object"
	    set output_file "[file rootname [file tail $prog]].o"
	}
	"link" {
	    set compile_type "executable"
	    set output_file "./[file rootname [file tail $prog]]"
	}
	"run" {
	    set compile_type "executable"
	    # FIXME: "./" is to cope with "." not being in $PATH.
	    # Should this be handled elsewhere?
	    # YES.
	    set output_file "./[file rootname [file tail $prog]]"
	    # This is the only place where we care if an executable was
	    # created or not.  If it was, dg.exp will try to run it.
	    remote_file build delete $output_file;
	}
	default {
	    perror "$do_what: not a valid dg-do keyword"
	    return ""
	}
    }
    set options ""
    if { $extra_tool_flags != "" } {
	lappend options "additional_flags=$extra_tool_flags"
    }

    set comp_output [libstdc++-v3_target_compile "$prog" "$output_file" "$compile_type" $options];

    return [list $comp_output $output_file]
}


proc libstdc++-v3_target_compile { source dest type options } {
    global gluefile 
    global wrap_flags
    global cxx
    global cxxflags
    global includes

    if { [target_info needs_status_wrapper] != "" && [info exists gluefile] } {
	lappend options "libs=${gluefile}"
	lappend options "ldflags=${wrap_flags}"
    }

    set cxx_final $cxx
    set cxxlibglossflags  [libgloss_link_flags]
    set cxx_final [concat $cxx_final $cxxlibglossflags]
    set cxx_final [concat $cxx_final $cxxflags]
    set cxx_final [concat $cxx_final $includes]

    lappend options "compiler=$cxx_final";

    return [target_compile $source $dest $type $options]
}

# On IRIX 6, we have to set variables akin to LD_LIBRARY_PATH, but
# called LD_LIBRARYN32_PATH (for the N32 ABI) and LD_LIBRARY64_PATH
# (for the 64-bit ABI).  The right way to do this would be to modify
# unix.exp -- but that's not an option since it's part of DejaGNU
# proper, so we do it here, by trickery.  We really only need to do 
# this on IRIX, but it shouldn't hurt to do it anywhere else.
proc libstdc++-v3_set_ld_library_path { name element op } {
  setenv LD_LIBRARYN32_PATH [getenv LD_LIBRARY_PATH]
  setenv LD_LIBRARY64_PATH [getenv LD_LIBRARY_PATH]
}

trace variable env(LD_LIBRARY_PATH) w libstdc++-v3_set_ld_library_path
